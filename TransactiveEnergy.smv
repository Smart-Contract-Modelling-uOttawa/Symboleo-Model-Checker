--------------------------------------------------------------------------------------
-- 'start' start timer from 0 to '_max_time_'.
--------------------------------------------------------------------------------------
MODULE Timer(start, _max_time_)
  DEFINE
	_time := time;

  VAR
	time : -1 .. _max_time_;

  ASSIGN
    init(time) 	:= -1;
    next(time) 	:= case
       time=-1 & start 		               	: 0;
       time>-1 & start & time < _max_time_ 	: time + 1;
       time = _max_time_ & start			: _max_time_;
       TRUE                                 : time;
    esac;

--------------------------------------------------------------------------------------
-- 'start' starts timer from 0 to '_max_time_'.
--------------------------------------------------------------------------------------
MODULE Event(started, _max_time_)
  DEFINE
	_inactive 	:= (state = inactive);
	_happened 	:= (state = happened);
	_expired 	:= (state = expired);

  VAR
	triggered	: boolean;
	timer 		: Timer(started & !_happened & !_expired, _max_time_);
	state 		: {inactive, active, happened, expired};
	performer	: {"CBEEF", "COSTCO"};

  ASSIGN
	init(triggered) := FALSE;
	next(triggered)	:= case
		state=active & started	: {FALSE, TRUE};
		TRUE	 				: FALSE;
	esac;

  ASSIGN
	init(performer) := "CBEEF";
	next(performer)	:= case
		state=active & started	: {"CBEEF", "COSTCO"};
		TRUE	 				: performer;
	esac;

  ASSIGN
	init(state)  	:= inactive;
	next(state)  	:= case
		state=inactive & started 										: active;
		state=active & started & triggered & timer._time<_max_time_ 	: happened;
		state=active & started & timer._time = _max_time_	   			: expired;
		TRUE					   	 			 					: state;
	esac;

--------------------------------------------------------------------------------------
-- 'name' is party name
-- 'removeL/R/P' releases liability, righHolder or performer position of a party
-- 'addL/R/P' adds liability, righHolder or performer position to a party
--------------------------------------------------------------------------------------
MODULE Party(norm, name, removeL, addL, removeR, addR, removeP, addP)
  DEFINE
	_name 			:= name;
	_norm			:= norm;
	_is_performer	:= p_state=P;
	_is_liable		:= l_state=L;
	_is_rightHolder	:= r_state=R;

  VAR
	l_state	: {Init, L};
	r_state	: {Init, R};
	p_state	: {Init, P};

  ASSIGN
	init(l_state) := Init;
	next(l_state)	:= case
		l_state=Init & addL	: L;
		l_state=L &removeL	: Init;
		TRUE				: l_state;
	esac;

  ASSIGN
	init(r_state) := Init;
	next(r_state)	:= case
		r_state=Init & addR	: R;
		r_state=R & removeR : Init;
		TRUE				: r_state;
	esac;

  ASSIGN
	init(p_state) := Init;
	next(p_state)	:= case
		p_state=Init & addP : P;
		p_state=P & removeP	: Init;
		TRUE				: p_state;
	esac;

  INVAR
	!(addL & removeL) &
	!(addR & removeR) &
	!(addP & removeP) &
	!(_is_rightHolder & _is_liable);

--------------------------------------------------------------------------------------
-- 'cnt_in_effect'    indicates if the contract is in inEffect state
-- 'power_suspended'  indicates if a power suspends the obligation
-- 'cnt_suspended'    indicates if the contract suspension suspends the obligation
-- 'power_resumed'    indicates if a power resumption resumes the obligation
-- 'cnt_resumed'      indicates if the contract resumption resumes the obligation
--------------------------------------------------------------------------------------
MODULE Obligation(surviving, cnt_in_effect, cnt_untermination,
                  fulfilled, triggered, violated, activated,
				  expired1, power_suspended, cnt_suspended, terminated, power_resumed, 
				  cnt_resumed, discharged, antecedent)
  DEFINE
	_surviving	:= surviving;
	_suspended 	:= (power_suspended | (cnt_suspended & !surviving));
	_active 	:= (state = inEffect | state = suspension);

  VAR
	state 		: {not_created, create, inEffect, suspension, discharge,
				   fulfillment, violation, unsTermination};
	sus_state	: {not_suspended, sus_by_contract, sus_by_power};

  ASSIGN
 --NEW: update axioms(surviving obligations are not suspended! informally mentioned)
	init(sus_state) := not_suspended;
	next(sus_state)	:= case
		sus_state=not_suspended & !surviving & cnt_suspended 	: sus_by_contract;
		sus_state=sus_by_contract & !surviving & cnt_resumed	: not_suspended;
		sus_state=not_suspended & !surviving & power_suspended 	: sus_by_power;
		sus_state=sus_by_power & !surviving & power_resumed		: not_suspended;
		TRUE													: sus_state;
	esac;

  ASSIGN
    init(state) := not_created;
    next(state) := case
		cnt_in_effect & state=not_created   & triggered & !antecedent : create;
		cnt_in_effect & state=not_created   & triggered & antecedent  : inEffect;
		cnt_in_effect & state=create        & antecedent              : inEffect;
		cnt_in_effect & state=create        & (expired1 | discharged)  : discharge;
		cnt_in_effect & state=inEffect      & fulfilled               : fulfillment;
		cnt_in_effect & state=inEffect      & _suspended              : suspension;
		cnt_in_effect & state=inEffect      & violated                : violation;
		cnt_in_effect & _active            	& terminated              : unsTermination;
		cnt_untermination & !surviving  	& _active           	  : unsTermination;
		sus_state=sus_by_contract & state=suspension  & cnt_resumed   : inEffect;
		sus_state=sus_by_power	  & state=suspension  & power_resumed   : inEffect;		 	 
		TRUE                                                          : state;
  esac;

--------------------------------------------------------------------------------------
-- 'cnt_in_effect'   indicates if the contract is in inEffect state
-- 'power_suspended' indicates if a power suspends the power
-- 'cnt_suspended'   indicates if the contract suspension suspends the power
-- 'power_resumed'   indicates if a power resumption resumes the power
-- 'cnt_resumed'     indicates if the contract resumption resumes the power
--------------------------------------------------------------------------------------
MODULE Power(cnt_in_effect, triggered, activated, expired1, power_suspended, cnt_suspended,
       	     terminated, exerted, power_resumed, cnt_resumed, antecedent)
  DEFINE
	_active 	 := (state = inEffect | state = suspension);
	_suspended 	 := (power_suspended | cnt_suspended);

  VAR
	state 		: {not_created, create, inEffect, suspension, sTermination, unsTermination};
	sus_state	: {not_suspended, sus_by_contract, sus_by_power};

  ASSIGN
	init(sus_state) := not_suspended;
	next(sus_state)	:= case
		sus_state=not_suspended & cnt_suspended 	: sus_by_contract;
		sus_state=sus_by_contract & cnt_resumed		: not_suspended;
		sus_state=not_suspended & power_suspended 	: sus_by_power;
		sus_state=sus_by_power & power_resumed		: not_suspended;
		TRUE										: sus_state;
	esac;

  ASSIGN
   init(state) := not_created;
   next(state) := case
     cnt_in_effect & state = not_created 	& triggered & !antecedent : create;
     cnt_in_effect & state = not_created 	& triggered & antecedent  : inEffect;
     cnt_in_effect & state = create      	& antecedent              : inEffect;
     cnt_in_effect & state = create      	& expired1                : unsTermination;
     cnt_in_effect & state = inEffect    	& exerted                 : sTermination;
     cnt_in_effect & state = inEffect   	& _suspended              : suspension;
     cnt_in_effect & state = inEffect    	& expired1                : unsTermination;
	 cnt_in_effect & _active  			 	& terminated              : unsTermination;

	 sus_state=sus_by_contract & state=suspension  & cnt_resumed 	  : inEffect;
	 sus_state=sus_by_power	   & state=suspension  & power_resumed      : inEffect;

     TRUE                                          				   	  : state;
   esac;

--------------------------------------------------------------------------------------
-- 'assigned_party'				 indicates if a party is assigned to a role
-- 'revoked_party'				 indicates if a party is unassigned from a role
-- 'fulfilled_active_obligation' indicates if all active obligations are fulfilled
--------------------------------------------------------------------------------------
MODULE Contract(triggered, activated, terminated, suspended, resumed,
				revoked_party, assigned_party, fulfilled_active_obligation)
  DEFINE
	_active 	 := (state = unassign | state = inEffect | state = suspension);
	_termination := (state = sTermination | state = unsTermination);

	-- obligations/powers' status changes once the contract goes to inEffect state
	_o_activated := (state = form & activated) |
						(state = suspension & resumed) |
						(state = unassign & assigned_party) |
						(state = inEffect);

  VAR
	state 		 : { not_created, form, inEffect, suspension, unassign, sTermination, unsTermination};

  ASSIGN
    init(state) := not_created;
    next(state) := case
		state = not_created & triggered  					: form;
		state = form        & activated  					: inEffect;
		state = inEffect    & fulfilled_active_obligation  	: sTermination;
		state = inEffect    & suspended  					: suspension;
		state = inEffect    & revoked_party  				: unassign;
		state = inEffect    & terminated 					: unsTermination;
		state = suspension  & resumed    					: inEffect;
		state = suspension  & terminated 					: unsTermination;
		state = unassign    & assigned_party 				: inEffect;
		state = unassign    & terminated 					: unsTermination;
		TRUE                             					: state;
    esac;
	

--------------------------------------------------------------------------------------
-- Domain dependent events
--------------------------------------------------------------------------------------	
MODULE Paid(started, _max_time_, price)
	DEFINE
		_price := amount;
	VAR
		amount : 0 .. price;
		event  : Event(started, _max_time_);
		
	ASSIGN
		init(amount) := 0;
		next(amount) := case
			event._happened: price;
			TRUE	 	   : amount;
		esac;
		
MODULE Supplied(started, _max_time_, energy)
	DEFINE
		_energy := amount;
	VAR
		amount: 0 .. energy;
		event : Event(started, _max_time_);
		
	ASSIGN
		init(amount) := 0;
		next(amount) := case
			event._happened: energy;
			TRUE	 	   : amount;
		esac;

--------------------------------------------------------------------------------------
-- Sales of Goods contract
--------------------------------------------------------------------------------------
MODULE TE_Contract(caiso, derp, sup_due_hours)

  DEFINE
	FINE 		  		:= 5;
	PRICE_PER_KWH 		:= 1;
	ENERGY_AMOUNT		:= 2000;
	sCTerminated 		:= !(OsupplyEnergy1._active) & !(OsupplyEnergy2._active) & !(OsupplyEnergy3._active) & 
					!(OpayByDerp1._active) & !(OpayByDerp2._active) & !(OpayByDerp3._active) & 
					!(OpayByIso1._active) & !(OpayByIso2._active) & !(OpayByIso3._active);
	PterCntByIso_exerted := PterCntByIso._active & terminated_C._happened;
	PterCntByDerp_exerted:= PterCntByDerp._active & terminated_C._happened;
	
	PterCnt_exerted		:= (PterCntByIso._active | PterCntByDerp._active) & terminated_C._happened;
	
	creditInvoiceIssued1:= OpayByIso1.state=fulfillment;
	creditInvoiceIssued2:= OpayByIso2.state=fulfillment;
	creditInvoiceIssued3:= OpayByIso3.state=fulfillment;
	
	OpayByIso1_violated	:= OpayByIso1._active & isoPaid1.event._expired;
	OpayByIso2_violated	:= OpayByIso2._active & isoPaid2.event._expired;
	OpayByIso3_violated	:= OpayByIso3._active & isoPaid3.event._expired;
	
	Osupply1_violated	:= OsupplyEnergy1._active & energySupplied1.event._expired;
	Osupply2_violated	:= OsupplyEnergy2._active & energySupplied2.event._expired;
	Osupply3_violated	:= OsupplyEnergy3._active & energySupplied3.event._expired;
	
	OpayByDerp1_violated	:= OpayByDerp1._active & derpPaid1.event._expired;
	OpayByDerp2_violated	:= OpayByDerp2._active & derpPaid2.event._expired;
	OpayByDerp3_violated	:= OpayByDerp3._active & derpPaid3.event._expired;
	
	PimposePenalty1_exerted	 := PimposePenalty1._active & penalized1._happened;
	PimposePenalty2_exerted	 := PimposePenalty2._active & penalized2._happened;
	PimposePenalty3_exerted	 := PimposePenalty3._active & penalized3._happened;
  
  VAR 
	bidAccepted1		 : boolean;
	bidAccepted2		 : boolean;
	bidAccepted3		 : boolean;
	
	energySupplied1 	: Supplied(OsupplyEnergy1.state=inEffect, sup_due_hours, ENERGY_AMOUNT);
	energySupplied2 	: Supplied(OsupplyEnergy2.state=inEffect, sup_due_hours, ENERGY_AMOUNT);
	energySupplied3 	: Supplied(OsupplyEnergy3.state=inEffect, sup_due_hours, ENERGY_AMOUNT);
	
	isoPaid1 			: Paid(OpayByIso1.state=inEffect, 4, PRICE_PER_KWH * ENERGY_AMOUNT);
	isoPaid2 			: Paid(OpayByIso2.state=inEffect, 4, PRICE_PER_KWH * ENERGY_AMOUNT);
	isoPaid3 			: Paid(OpayByIso3.state=inEffect, 4, PRICE_PER_KWH * ENERGY_AMOUNT);
--	isoPaid1 			: Paid(OpayByIso1.state=inEffect, 4, PRICE_PER_KWH * energySupplied1._energy);
--	isoPaid2 			: Paid(OpayByIso2.state=inEffect, 4, PRICE_PER_KWH * energySupplied2._energy);
--	isoPaid3 			: Paid(OpayByIso3.state=inEffect, 4, PRICE_PER_KWH * energySupplied3._energy);
	
--	derpPaid1 			: Event(OpayByDerp1.state=inEffect, noticed1.timer._time+30);
--	derpPaid2 			: Event(OpayByDerp2.state=inEffect, noticed2.timer._time+30);
--	derpPaid3 			: Event(OpayByDerp3.state=inEffect, noticed3.timer._time+30);
--	derpPaid1 			: Paid(OpayByDerp1.state=inEffect, 30, isoPaid1._price * FINE);
--	derpPaid2 			: Paid(OpayByDerp2.state=inEffect, 30, isoPaid2._price * FINE);
--	derpPaid3 			: Paid(OpayByDerp3.state=inEffect, 30, isoPaid3._price * FINE);
	derpPaid1 			: Paid(OpayByDerp1.state=inEffect, 30, PRICE_PER_KWH * ENERGY_AMOUNT * FINE);
	derpPaid2 			: Paid(OpayByDerp2.state=inEffect, 30, PRICE_PER_KWH * ENERGY_AMOUNT * FINE);
	derpPaid3 			: Paid(OpayByDerp3.state=inEffect, 30, PRICE_PER_KWH * ENERGY_AMOUNT * FINE);

--	derpPaid1 			: Event(OpayByDerp1.state=inEffect, 30);
--	derpPaid2 			: Event(OpayByDerp2.state=inEffect, 30);
--	derpPaid3 			: Event(OpayByDerp3.state=inEffect, 30);
		
	penalized1 			: Event(PimposePenalty1.state = inEffect, 2);
	penalized2			: Event(PimposePenalty2.state = inEffect, 2);
	penalized3			: Event(PimposePenalty3.state = inEffect, 2);
	
--	noticed1 			: Event(OpayByDerp1_violated, 2);
--	noticed2			: Event(OpayByDerp2_violated, 2);
--	noticed3			: Event(OpayByDerp3_violated, 2);

	terminated_C   		: Event(PterCntByIso.state = inEffect | PterCntByDerp.state = inEffect, 4);
	
	C 		 		 : Contract(TRUE, TRUE, PterCnt_exerted, FALSE, FALSE, FALSE, FALSE, sCTerminated);
	
	OsupplyEnergy1 	 : Obligation(FALSE, C._o_activated, PterCnt_exerted, energySupplied1.event._happened, 
                  bidAccepted1, Osupply1_violated, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, TRUE);
				  
	OsupplyEnergy2 	 : Obligation(FALSE, C._o_activated, PterCnt_exerted, energySupplied2.event._happened, 
                  bidAccepted2, Osupply2_violated, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, TRUE);
				 
	OsupplyEnergy3 	 : Obligation(FALSE, C._o_activated, PterCnt_exerted, energySupplied3.event._happened, 
                  bidAccepted3, Osupply3_violated, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, TRUE);
				  
				  
	OpayByIso1 	 	 : Obligation(FALSE, C._o_activated, PterCnt_exerted, isoPaid1.event._happened, 
					creditInvoiceIssued1, OpayByIso1_violated, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, TRUE);
	OpayByIso2 	 	 : Obligation(FALSE, C._o_activated, PterCnt_exerted, isoPaid2.event._happened, 
					creditInvoiceIssued2, OpayByIso2_violated, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, TRUE);
	OpayByIso3 	 	 : Obligation(FALSE, C._o_activated, PterCnt_exerted, isoPaid3.event._happened, 
					creditInvoiceIssued3, OpayByIso3_violated, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, TRUE);
		   
				   				  
	OpayByDerp1 	 : Obligation(FALSE, C._o_activated, PterCnt_exerted, derpPaid1.event._happened, 
                  PimposePenalty1_exerted, OpayByDerp1_violated, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, TRUE);
				  
	OpayByDerp2 	 : Obligation(FALSE, C._o_activated, PterCnt_exerted, derpPaid2.event._happened, 
                  PimposePenalty2_exerted, OpayByDerp2_violated, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, TRUE);

	OpayByDerp3 	 : Obligation(FALSE, C._o_activated, PterCnt_exerted, derpPaid3.event._happened, 
                  PimposePenalty3_exerted, OpayByDerp3_violated, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, FALSE, TRUE);		
			 
	PimposePenalty1 : Power(C._o_activated, Osupply1_violated, FALSE, FALSE, FALSE,
			   FALSE, PterCnt_exerted, PimposePenalty1_exerted, FALSE, FALSE, TRUE);
		
	PimposePenalty2 : Power(C._o_activated, Osupply2_violated, FALSE, FALSE, FALSE,
			   FALSE, PterCnt_exerted, PimposePenalty2_exerted, FALSE, FALSE, TRUE);
			   
	PimposePenalty3 : Power(C._o_activated, Osupply3_violated, FALSE, FALSE, FALSE,
			   FALSE, PterCnt_exerted, PimposePenalty3_exerted, FALSE, FALSE, TRUE);
	
	PterCntByIso  : Power(C._o_activated, TRUE, FALSE, FALSE, FALSE, FALSE, PterCnt_exerted, PterCntByIso_exerted, 	
			   FALSE, FALSE, OpayByDerp1_violated | OpayByDerp2_violated | OpayByDerp3_violated);
	
	PterCntByDerp  : Power(C._o_activated, TRUE, FALSE, FALSE, FALSE, FALSE, PterCnt_exerted, 
			   PterCntByDerp_exerted, FALSE, FALSE, TRUE);
				   
  INVAR
	caiso != derp;
		
MODULE main
  CONSTANTS 
	"caiso", "derp";
  VAR 
	te_cnt 	: TE_Contract("caiso", "derp", 4);
		
  ASSIGN
	------------------------------------------------------------------------------------
	-- Generic liveness
	------------------------------------------------------------------------------------
	--* Number	   : 1.0
	--* Description: a contract eventually terminates
	--* Type 	   : desirable property
	--* Pattern	   : Existence pattern #31
	LTLSPEC NAME LTL1 := F(te_cnt.C.state = sTermination | te_cnt.C.state = unsTermination)
	
	
	------------------------------------------------------------------------------------
	-- Specific safety
	------------------------------------------------------------------------------------
	--* Number	   : 2.1
	--* Description: caiso cannot terminate a contract if the derp supplies committed energy
	--* Type 	   : undesirable property
	--* Pattern	   : Absence pattern #13
	LTLSPEC NAME LTL2 := G(te_cnt.OsupplyEnergy1.state=fulfillment &
						te_cnt.OsupplyEnergy1.state=fulfillment &
						te_cnt.OsupplyEnergy1.state=fulfillment -> G!(te_cnt.PterCntByIso_exerted));
						
	--* Number	   : 2.2
	--* Description: derp never pays less than twice as much as energy fee
	--* Type 	   : undesirable property
	--* Pattern	   : Exclusion pattern #1
	CTLSPEC NAME LTL3 := !EF(te_cnt.derpPaid1._price + te_cnt.derpPaid2._price + te_cnt.derpPaid3._price > 
						(te_cnt.energySupplied1._energy + te_cnt.energySupplied2._energy + te_cnt.energySupplied3._energy) * te_cnt.PRICE_PER_KWH);
